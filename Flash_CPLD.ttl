;////// FLASH CPLD //////
;
; This script is used for flashing CPLD.
; Start from $Start_Layer to $End_Layer;
; Make sure the setup items are correct before execution.
;
;========================
;////// Setup Item //////
RM_IP='192.168.0.101'
Start_Layer=8
End_Layer=17
ROM_File='20201008_C2080_V05.pof'
;========================
;/// Connect to RM IP ///
strconcat RM_Connect RM_IP
strconcat RM_Connect ' /ssh /auth=password /user=root /passwd=$pl3nd1D'
;========================

;===========
:Start_Flash
;===========

i = Start_Layer
do while i <= End_Layer
	connect RM_Connect
	pause 10
	sprintf2 flashcpld "set system cpld update -i %d -f " i
    strconcat flashcpld ROM_File
	sendln flashcpld
	pause 60
	disconnect 0
	pause 10
	i = i+1
loop

pause 1800

;========
:AC_Reset
;========

connect RM_Connect

i = Start_Layer
do while i <= End_Layer
	pause 10
	sprintf2 poweroff "set manager port off -i %d -f" i
	sendln poweroff
	pause 10
	i = i+1
loop

pause 300

i = Start_Layer
do while i <= End_Layer
	pause 10
	sprintf2 poweron "set manager port on -i %d" i
	sendln poweron
	pause 10
	i = i+1	
loop

disconnect 0

pause 300

;==============
:Check_Firmware
;==============

connect RM_Connect

i = Start_Layer
do while i <= End_Layer
	pause 10
	sprintf2 checkfirmware "show system info -i %d" i
	sendln checkfirmware
	pause 10
	i = i+1
loop

